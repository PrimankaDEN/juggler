apply plugin: 'com.android.library'
apply plugin: 'maven'

def groupId = "me.ilich.juggler"
def artifactId = "juggler"
def version = "0.0.9"

def releaseDir = "${buildDir}/release/${version}"

ext {
    bintrayRepo = 'maven'
    bintrayName = 'juggler'

    publishedGroupId = 'me.ilich.juggler'
    libraryName = 'Juggler'
    artifact = 'juggler'

    libraryDescription = 'Represents android navigation as a state machine.'

    siteUrl = 'https://github.com/dsokolov/juggler'
    gitUrl = 'https://github.com/dsokolov/juggler.git'

    libraryVersion = '0.0.8'

    developerId = 'dsokolov'
    developerName = 'Dmitry Sokolov'
    developerEmail = 'DmitryIlichSokolov@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

configurations {
    javadocDependencies
}

task javaDocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.javadocDependencies
}

task javaDocsJar(type: Jar, dependsOn: javaDocs) {
    classifier = 'javadoc'
    from javaDocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

uploadArchives {
    repositories.mavenDeployer {
        pom.groupId = groupId
        pom.artifactId = artifactId
        pom.version = version
        repository(url: "file://${releaseDir}")
    }
}

artifacts {
    archives sourcesJar
    archives javaDocsJar
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:support-annotations:23.1.1'
    javadocDependencies 'com.android.support:appcompat-v7:23.1.1'
    javadocDependencies 'com.android.support:support-annotations:23.1.1'
}

//apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
//apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'